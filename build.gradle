buildscript {
    ext {
        set('springBootVersion', "2.2.9.RELEASE")
        set('springCloudVersion', "Hoxton.SR7")
        set('springCloudAlibabaVersion', "2.2.1.RELEASE")
    }

    repositories {
        maven{ url 'http://maven.aliyun.com/nexus/content/groups/public/'}
    }

    dependencies {
        classpath 'io.spring.gradle:dependency-management-plugin:1.0.9.RELEASE'
        classpath ("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
    }
}

subprojects {
    apply plugin: 'java'
    apply plugin: 'idea'
    apply plugin: 'org.springframework.boot'
    apply plugin: 'io.spring.dependency-management'

    group = 'cn.ywb.springcloud'
    version = '1.0.0'
    sourceCompatibility = '1.8'
    targetCompatibility = '1.8'
    [compileJava, compileTestJava, javadoc]*.options*.encoding = 'UTF-8'

    jar.enabled = true
    bootJar.enabled = false

    configurations {
        developmentOnly
        runtimeClasspath {
            extendsFrom developmentOnly
        }
        compileOnly {
            extendsFrom annotationProcessor
        }
    }

    repositories {
        maven{ url 'http://maven.aliyun.com/nexus/content/groups/public/'}
    }

    dependencyManagement {
        imports {
            mavenBom "org.springframework.boot:spring-boot-dependencies:${springBootVersion}"
            mavenBom "org.springframework.cloud:spring-cloud-dependencies:${springCloudVersion}"
            mavenBom "com.alibaba.cloud:spring-cloud-alibaba-dependencies:${springCloudAlibabaVersion}"
        }
    }

    dependencies {
        implementation 'io.springfox:springfox-swagger2:2.10.5'
        implementation 'io.springfox:springfox-swagger-ui:2.10.5'
        implementation 'io.springfox:springfox-spring-webflux:2.10.5'
        compileOnly 'org.projectlombok:lombok'
        developmentOnly 'org.springframework.boot:spring-boot-devtools'
        annotationProcessor 'org.springframework.boot:spring-boot-configuration-processor'
        annotationProcessor 'org.projectlombok:lombok'
        testImplementation('org.springframework.boot:spring-boot-starter-test') {
            exclude group: 'org.junit.vintage', module: 'junit-vintage-engine'
        }
    }

    test {
        useJUnitPlatform()
    }
}
